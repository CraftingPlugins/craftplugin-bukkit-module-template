//file:noinspection GroovyAssignabilityCheck
buildscript {
    repositories {
        mavenCentral()
    }
}
plugins {
    id 'net.kyori.blossom' version '2.0.0'
    id "org.jetbrains.gradle.plugin.idea-ext" version "1.1.7"
    id 'io.fairyproject' version '0.6.7b2-SNAPSHOT'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'xyz.jpenilla.run-paper' version '1.0.6' apply false
}

repositories {
    mavenLocal()
    mavenCentral()
}

version = '1.0.0'

allprojects {
    if (isNotJavaProject(project)) {
        project.tasks.configureEach { task -> task.enabled = false }
        return
    }

    group = findProperty("plugin.package")
}

subprojects {
    version = rootProject.version

    if (isNotJavaProject(project)) {
        project.tasks.configureEach { task -> task.enabled = false }
        project.tasks.withType(PublishToMavenRepository).configureEach { it.enabled = false }
        return
    }

    apply plugin: 'java-library'
    apply plugin: 'io.fairyproject'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'net.kyori.blossom'
    apply plugin: 'org.jetbrains.gradle.plugin.idea-ext'

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
        maven {
            name 'Jitpack'
            url 'https://jitpack.io'
        }
        maven {
            name 'Imanity'
            url 'https://repo.imanity.dev/imanity-libraries'
        }
        maven {
            name 'PaperMC'
            url 'https://repo.papermc.io/repository/maven-public'
        }
        maven {
            name = "CodeMC"
            url = uri("https://repo.codemc.io/repository/maven-public/")
        }
        maven {
            name = "Ghast"
            url "https://repo.ghast.dev/private"
        }
    }

    dependencies {
        // Manifold
        api 'systems.manifold:manifold-ext-rt:2023.1.19'
        annotationProcessor group: 'systems.manifold', name: 'manifold-ext', version: '2023.1.19'
        testAnnotationProcessor group: 'systems.manifold', name: 'manifold-ext', version: '2023.1.19'

        annotationProcessor 'systems.manifold:manifold-strings:2023.1.18'
        testAnnotationProcessor 'systems.manifold:manifold-strings:2023.1.18'

        // Lombok
        compileOnly(libs.lombok)
        annotationProcessor(libs.lombok)
        testCompileOnly(libs.lombok)
        testAnnotationProcessor(libs.lombok)

        // Testing
        testImplementation("org.junit.jupiter:junit-jupiter-engine:5.9.0")
    }

    if (JavaVersion.current() != JavaVersion.VERSION_1_8 &&
            sourceSets.main.allJava.files.any { it.name == "module-info.java" }) {
        tasks.withType(JavaCompile) {
            // if you DO define a module-info.java file:
            options.compilerArgs += ['-Xplugin:Manifold', '--module-path', it.classpath.asPath]
        }
    } else {
        tasks.withType(JavaCompile) {
            // If you DO NOT define a module-info.java file:
            options.compilerArgs += ['-Xplugin:Manifold']
        }
    }

    java {
        toolchain {
            languageVersion.set(JavaLanguageVersion.of(8))
        }
    }

    sourceSets {
        main {
            blossom {
                javaSources {
                    property("version", project.version.toString())
                    property("package", findProperty("plugin.package").toString())
                }
            }
        }
    }

    shadowJar {
        relocate("io.fairyproject.bootstrap", "" + findProperty("plugin.package") + ".fairy.bootstrap")
        relocate("io.fairyproject", "" + findProperty("core.package") + ".fairy")
        relocate("net.kyori", "" + findProperty("core.package") + ".fairy.libs.kyori")
        relocate("com.cryptomorin.xseries", "" + findProperty("core.package") + ".xseries")
        relocate("org.spongepowered.configurate", "" + findProperty("plugin.package") + ".libs.configurate")

        dependencies {
            exclude(dependency("io.github.classgraph:.*:.*"))
            exclude(dependency("io.github.toolfactory:.*:.*"))
            exclude(dependency("javax.annotation:.*:.*"))
            exclude(dependency("javax.persistence:.*:.*"))
            exclude(dependency("com.google.code.gson:.*:.*"))
            exclude(dependency("org.yaml:.*:.*"))
            exclude(dependency("com.google.*:.*:.*"))
            exclude(dependency("org.jetbrains:.*:.*"))
            exclude(dependency("net.kyori:.*:.*"))
            exclude(dependency("com.github.retrooper.*:.*:.*"))
            exclude(dependency("io.fairyproject:.*-platform:.*"))
        }

        archiveFileName.set(archiveFileName.get().split("-")[0] + ".jar")
    }

    build {
        dependsOn(shadowJar)
    }

    test {
        useJUnitPlatform()
    }
}

static boolean isNotJavaProject(Project project) {
    return project.buildFile == null || !project.buildFile.exists()
}
